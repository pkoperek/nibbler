apply plugin: 'scala'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'application'

// jetty runner class
mainClassName = 'org.eclipse.jetty.runner.Runner'

configurations {
    container // group for jetty container used to run webapp from ./gradlew jettyRun
}

repositories
{
    mavenCentral()

    maven {
        url "http://repo.spray.io/"
    }

}

dependencies {
    // scala! 
    compile group: 'org.scala-lang', name: 'scala-library', version: '2.10'

    // scalatra deps
    compile group: 'org.scalatra', name: 'scalatra_2.10', version: '2.3.0'
    compile group: 'org.scalatra', name: 'scalatra-scalate_2.10', version: '2.3.0'

    // spray json
    compile group: 'io.spray', name: 'spray-json_2.10', version: '1.2.6'

    // container used to run from gradle
    container group: 'org.eclipse.jetty', name: 'jetty-runner', version: '9.2.1.v20140609'

    // servlet api
    providedCompile group: 'org.eclipse.jetty.orbit', name: 'javax.servlet', version: '3.0.0.v201112011016'

    // spark 
    providedCompile ('org.apache.spark:spark-core_2.10:1.0.0') {
        // need to turn off transitive deps due to:
        // http://forums.gradle.org/gradle/topics/war_plugin_not_including_certain_compile_dependencies_in_war
        transitive = false
    }

    // hadoop-client
    providedCompile group: 'org.apache.hadoop', name: 'hadoop-client', version: '2.2.0'
}

// manually specify gradle version for gradlew
task wrapper(type: Wrapper) {
    distributionUrl = "http://services.gradle.org/distributions/gradle-2.0-bin.zip"
}

// runs jetty 
task jettyRun(type: JavaExec) {
    main = "org.eclipse.jetty.runner.Runner"
    args = [war.archivePath]
    classpath configurations.container
}

// include war file
applicationDistribution.from(war) {
    into("webapp")
}

applicationDistribution.from('build/container') {
    into("lib")
}

// prepare container 
task prepareContainer(type: Copy) {
    from configurations.container
    into 'build/container'
}

startScripts {
    classpath = configurations.container

    doLast {
        unixScript.text = unixScript.text.replace("$mainClassName", "$mainClassName webapp/$war.archiveName")
    }
}

distZip {
    // remove all jars added by default by application plugin
    def toExclude = [jar.archiveName]
    configurations.runtime.each { dependency ->
        toExclude.add(dependency.getName())
    }
    excludes = toExclude
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
        scopes.PROVIDED.plus += [configurations.providedCompile]
    }
}

distZip.dependsOn prepareContainer
jettyRun.dependsOn war
